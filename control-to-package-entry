#!/usr/bin/env perl

# $Id$

use strict;
use warnings;

my (%attributes, $last_attribute);
$attributes{version} = q{XXX-VERSION-XXX};

LINE:
while (<>) {
    m/^\s*$/ and next LINE;
    chomp;
    if (m/^(\S+): +(.*)/) {
        $last_attribute = lc $1;
        $attributes{$last_attribute} = $2;
    } else {
        $attributes{$last_attribute} .= qq{\n} . $_;
    }
}

my $output = <<"OUTPUT";
[[!template id="debian-package"
OUTPUT
foreach my $attribute (qw(package version section architecture homepage
    depends)) {
    $output .= qq{$attribute="$attributes{$attribute}"\n};
}

foreach my $attribute (qw(recommends suggests)) {
    if (exists $attributes{$attribute}) {
        $output .= qq{$attribute="$attributes{$attribute}"\n};
    }
}

$output .= <<"FILES";
tarball="$attributes{package}_$attributes{version}.orig.tar.gz"
deb="$attributes{package}_$attributes{version}.deb"
diff="$attributes{package}_$attributes{version}.diff.gz"
FILES

my ($description_short, $description_long)
    = split /\n/, $attributes{description}, 2;
$output .= qq{description_short="$description_short"\n};
if ($description_long) {
    $output .= qq{description_long="""$description_long"""\n}
}

$output .= qq{]]\n};
print $output;
